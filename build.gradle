plugins {
    id 'org.graalvm.buildtools.native' version '0.9.4'
    id 'java'
}

group 'com.funbiscuit'
version '0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.pdfbox:pdfbox:2.0.24'
    implementation 'org.apache.pdfbox:jbig2-imageio:3.0.3'
    implementation 'com.github.jai-imageio:jai-imageio-jpeg2000:1.4.0'
    implementation 'com.github.jai-imageio:jai-imageio-core:1.4.0'

    implementation 'info.picocli:picocli:4.6.1'
    annotationProcessor 'info.picocli:picocli-codegen:4.6.1'

    implementation 'me.tongfei:progressbar:0.9.2'

    implementation 'org.scala-lang:scala3-library_3:3.1.0'

    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

compileJava {
    options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

jar {
    archiveFileName.set("${rootProject.getName()}.${archiveExtension.get()}")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE // exclude duplicates
    manifest {
        attributes(
                'Main-Class': 'com.funbiscuit.pdfconvert.PdfMain'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

nativeBuild {
    mainClass = 'com.funbiscuit.pdfconvert.PdfMain'

    buildArgs.add('-H:+AddAllCharsets')
    buildArgs.add('--allow-incomplete-classpath')
}

test {
    useJUnitPlatform()
}
